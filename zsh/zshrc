# TODO(strager): Move into ~/.zshenv.
fpath=(~/.zsh/strager "${fpath[@]}")
autoload -Uk strager_ftcs_sequence
autoload -Uk strager_initialize_post_prompt
autoload -Uk strager_iterm_preexec
autoload -Uk strager_iterm_sequence
autoload -Uk strager_iterm_wrap_prompt
autoload -Uk strager_prompt_cwd

# Saved history.
# N.B. Do not set HISTFILE here. HISTFILE is set by
# strager_initialize_history.
unset HISTFILE
STRAGER_HISTFILE=~/.zshhistory
HISTSIZE=100000
SAVEHIST=100000
setopt hist_find_no_dups
setopt inc_append_history
unsetopt hist_ignore_space

# Command interpretation.
setopt autocd
setopt chase_dots
setopt interactive_comments

# Keyboard bindings.
bindkey -e

# TODO(strager): select-word-style bash (except for ^W).

bindkey "${terminfo[kdch1]}" delete-char

autoload -U up-line-or-beginning-search
zle -N up-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search # Up

autoload -U down-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[B" down-line-or-beginning-search # down

autoload -U edit-command-line
zle -N edit-command-line
bindkey '\C-x\C-e' edit-command-line

# Completion.
# N.B. Do not call compinit here. compinit is called by
# strager_initialize_completion.
unsetopt auto_remove_slash

# Environment.
export EDITOR=vim
export PATH="${HOME}/bin:${PATH}"
export PATH="${HOME}/.cargo/bin:${PATH}"
# NOTE(strager): strager_initialize_nix adds
# ~/.nix-profile/bin to $PATH, among other things.

# The prompt:
#
#     ~/Applications> false
#     ~/Applications 1> cd /tmp
#     /tmp>
setopt prompt_subst
PROMPT=
# Current directory.
PROMPT="${PROMPT}%F{green}\$(strager_prompt_cwd)%f"
# Exit code of previous command.
PROMPT="${PROMPT}%(0?;; %F{red}%?%f)"
# Terminator.
PROMPT="${PROMPT}> "
# iTerm 2 integration.
PROMPT="$(strager_iterm_wrap_prompt "${PROMPT}")"
preexec_functions=(strager_iterm_preexec "${preexec_functions[@]}")

# Make ls print colours by default.
ls() {
    /bin/ls -G "${@}"
}

# Git aliases.
gs() {
    git status -s -b "${@}"
}
gc() {
    git commit -v "${@}"
}
g.() {
    git add -p "${@}"
}
gd() {
    git diff "${@}"
}
gp() {
    git checkout -p "${@}"
}
gr() {
    git rebase "${@}"
}
grc() {
    git rebase --continue "${@}"
}
gar() {
    git add --all .
}
ga() {
    git commit --amend --reuse-message=HEAD
}

title() {
    printf '\x1b]1;%s\x07' "${1}"
}

# Input styling.
ZSH_HIGHLIGHT_HIGHLIGHTERS=(main)
typeset -A ZSH_HIGHLIGHT_STYLES
# TODO(strager): Fix the following issues:
# * https://github.com/zsh-users/zsh-syntax-highlighting/issues/239
# * https://github.com/zsh-users/zsh-syntax-highlighting/issues/202
# TODO(strager): Underline /absolute/path commands but not
# $PATH commands.
_command_style='fg=26'
_argument_style='fg=39'
_error_style='fg=red'
ZSH_HIGHLIGHT_STYLES[alias]="${_command_style}"
ZSH_HIGHLIGHT_STYLES[arg0]="${_command_style}"
ZSH_HIGHLIGHT_STYLES[builtin]="${_command_style}"
ZSH_HIGHLIGHT_STYLES[command]="${_command_style},underline"
ZSH_HIGHLIGHT_STYLES[comment]='fg=10'
ZSH_HIGHLIGHT_STYLES[default]="${_argument_style}"
ZSH_HIGHLIGHT_STYLES[dollar-quoted-argument-unclosed]="${_error_style}"
ZSH_HIGHLIGHT_STYLES[double-hypen-option]="${_argument_style}"
ZSH_HIGHLIGHT_STYLES[double-quoted-argument-unclosed]="${_error_style}"
ZSH_HIGHLIGHT_STYLES[function]="${_command_style}"
ZSH_HIGHLIGHT_STYLES[hashed-command]="${_command_style},underline"
ZSH_HIGHLIGHT_STYLES[path]="${_argument_style},underline"
ZSH_HIGHLIGHT_STYLES[precommand]="${_command_style}"
ZSH_HIGHLIGHT_STYLES[reserved-word]='fg=093'
ZSH_HIGHLIGHT_STYLES[single-hyphen-option]="${_argument_style}"
ZSH_HIGHLIGHT_STYLES[single-quoted-argument-unclosed]="${_error_style}"
# N.B. Do not source zsh-syntax-highlighting.zsh here. That
# script is sourced by
# strager_initialize_zsh_syntax_highlighting.

# N.B. Register zle-line-init after loading
# zsh-syntax-highlighting to avoid zsh-syntax-highlighting's
# wrappers.
if [[ "${STRAGER_INITIALIZE_POST_PROMPT:-1}" -eq 1 ]]; then
    strager_zle-line-init() {
        strager_initialize_post_prompt
        zle -D zle-line-init
    }
    zle -N zle-line-init strager_zle-line-init
fi

# Configure other programs.
export FZF_DEFAULT_COMMAND='rg --files --follow --hidden --no-ignore'
export FZF_DEFAULT_OPTS='--no-progress --inline-info'
